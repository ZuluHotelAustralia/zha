/* Unholy Spell - Impaling Bones **
** Unholy damage to the target.  **
** Seravy - seravy@gmail.com     */

use uo;
use os;

include "newclassbook";

const SPELL_ID := SPELL_ID_IMPALINGBONES;
// For Resisted()
const SPELL_EFFECT_TYPE_MOVING := FX_SPELL_IMPALINGBONES;
const SPELL_EFFECT_ID		 := FX_SPELL_IMPALINGBONES;
const SPELL_EFFECT_SPEED	 := SPEED_SPELL_IMPALINGBONES;
const SPELL_EFFECT_LOOP		 := LOOP_SPELL_IMPALINGBONES;

program cast_impalingbones(parms)
  var circle := GetCircle( SPELL_ID);
  var plane := NECRO;
  var caster;
  var cast_on;
  var fromhit;

  if (parms[1] == "#MOB")
    caster := parms[2];
    cast_on := parms[3];
    if (parms[4])
      circle := parms[4];
    endif
    if (parms[5])
      fromhit := parms[5];
    endif
    Send_Attack( cast_on, caster, SPELL_ID);
  else
    caster := parms;
    cast_on := CanTargetSpell( caster, circle, TGTOPT_HARMFUL + TGTOPT_CHECK_LOS );

    if (!cast_on)
      EraseObjProperty( caster, "#Casting");
      return;
    endif
    if (AutoCallGuardCheck(caster, cast_on))
      EraseObjProperty( caster, "#Casting");
      return;
    endif
  endif

  if( !fromhit )
    cast_on := Reflected( caster, cast_on, circle);
  endif
  PlayMovingEffect( caster, cast_on, FX_SPELL_IMPALINGBONES, SPEED_SPELL_IMPALINGBONES, LOOP_SPELL_IMPALINGBONES);
  PlaySoundEffect( cast_on, SFX_SPELL_IMPALINGBONES);

  var immunity := IsProtected(caster, cast_on, circle);
  if( immunity == IMMUNED )
    EraseObjProperty(caster, "#Casting");
    return;
  endif
  if( immunity == CURSED )
    circle := circle * 2;
  endif

  var dmg := CInt(CalcSpellDamage( caster, cast_on, circle, SINGLE_TARGET_SPELL, SKILLID_NECROMANCY));

  set_critical(1);
  var bones1 := CreateItemAtLocation( cast_on.x, cast_on.y, cast_on.z, 0x1B0F, 1);
  var bones2 := CreateItemAtLocation( cast_on.x, cast_on.y, cast_on.z, 0x1B10, 1);
  bones1.movable := 0;
  bones2.movable := 0;
  SetObjProperty(bones1, "f", 1);
  SetObjProperty(bones2, "f", 1);
  set_critical(0);

  ApplyPlanarDamage(caster, cast_on, circle, dmg, plane);
  EraseObjProperty( caster, "#Casting");

  sleep(10);
  DestroyItem(bones1);
  sleep(5);
  DestroyItem(bones2);
endprogram
