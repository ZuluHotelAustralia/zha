/* Unholy Spell - Apocalypse         **
** Unholy damage to all in the area. **
** Seravy - seravy@gmail.com         */

use uo;
use os;

include "newclassbook";
include "include/packets";

const SPELL_ID := SPELL_ID_APOCALYPSE;

program cast_apocalypse(parms)
  var caster;
  var circle := GetCircle(SPELL_ID);
  var plane := NECRO;
  var cast_on;

  if (parms[1] == "#MOB")
    caster := parms[2];
    cast_on := parms[3];
    if (parms[4])
      circle := parms[4];
    endif
  else
    caster := parms;
    cast_on := CanTargetArea(caster, circle);

    if (!cast_on)
      EraseObjProperty(caster, "#Casting");
      return;
    endif
  endif

  var range := calcDmgRange(caster, SKILLID_NECROMANCY);
  var dmg;

  var seedesolation := ListMobilesNearLocation(cast_on.x, cast_on.y, cast_on.z, 20);
  foreach person in seedesolation
    SetSeasonForPlayer(person, SEASON_DESOLATION);
  endforeach
  detach();
  PlaySoundEffect(caster, SFX_SPELL_APOCALYPSE);
  sleepms(500);

  EraseObjProperty( caster, "#Casting");
  PlayObjectCenteredEffect(caster, FX_SPELL_APOCALYPSE, SPEED_SPELL_APOCALYPSE, LOOP_SPELL_APOCALYPSE);
  var victims := ListMobilesNearLocation(cast_on.x, cast_on.y, cast_on.z, range);
  foreach mobile in victims
    if(!mobile.multi)
      var immunity := IsProtected( caster, mobile, circle );
      var circ := circle;
      if( immunity != IMMUNED )
        if( immunity == CURSED )
          circ := circ * 2;
        endif
        if (AutoCallGuardCheck(caster, mobile))
          EraseObjProperty( caster, "#Casting");
          foreach person in seedesolation
            SetSeasonForPlayer(person, SEASON_SPRING);
          endforeach
          return;
        endif

        dmg := (CalcSpellDamage( caster, mobile, circ, AREA_EFFECT_SPELL));
        send_attack(mobile, caster, SPELL_ID);
        PlayMovingEffectHuefx(caster, mobile, FX_SPELL_APOCALYPSE2, SPEED_SPELL_APOCALYPSE2, 
          LOOP_SPELL_APOCALYPSE2, 0, COLOR_SPELL_APOCALYPSE2);
        PlaySoundEffect(mobile, SFX_SPELL_APOCALYPSE2);

        ApplyPlanarDamage(caster, mobile, circle, dmg, plane);
      endif
    endif
  endforeach

  sleep(5);

  foreach person in seedesolation
    SetSeasonForPlayer(person, SEASON_SPRING);
  endforeach
  PlaySoundEffect(caster, SFX_SPELL_APOCALYPSE);
endprogram
