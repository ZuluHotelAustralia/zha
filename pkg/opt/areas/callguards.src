use uo;
use os;
use npc;
use cfgfile;

include "include/areas";

const SEARCH_RANGE := 8;

program call_guards_script( who )

	if( !CheckLastGuardCall(who))
		SendSysMessage( who, "You may only call the guards once every few seconds." );
		return 0;
	elseif( LookAround(who) )
		SendSysMessage( who, "The city guards have come to your rescue." );
	else
		SendSysMessage( who, "The guards didn't answer your call since there's nothing to protect you from." );
	endif

endprogram
		

function LookAround( who )

	var npc_cfg := ReadConfigFile(":*:npcdesc");
	var elem;
	var guard;
	var found := 0;

	foreach mobile in ListMobilesNearLocation( who.x, who.y, who.z, SEARCH_RANGE )
		if( mobile.IsA(POLCLASS_NPC) )
			elem := FindConfigElem( npc_cfg, mobile.npctemplate );
			if( (mobile.script=="tamed" && mobile.master.criminal)
			|| (mobile.script!="tamed" && !elem.guardignore && !GetObjProperty(mobile,"guardignore")))
				if( IsInGuardedArea(mobile) )
					set_critical(1);
					guard := CreateNpcFromTemplate( "guard", mobile.x, mobile.y, mobile.z );
					if( guard )
						SetObjProperty( guard, "target", mobile.serial );
					endif
					set_critical(0);
					found := 1;
				endif
			endif
		elseif( !mobile.cmdlevel )
			if( mobile.criminal )
				if( IsInGuardedArea(mobile) )
					set_critical(1);
					guard := CreateNpcFromTemplate( "guard", mobile.x, mobile.y, mobile.z );
					if( guard )
						SetObjProperty( guard, "target", mobile.serial );
					endif
					set_critical(0);
					found := 1;
				endif
			endif
		endif
	endforeach

	return found;
endfunction

function CheckLastGuardCall(who)
	//returns true if player hasnt called the guards in the last few seconds
	var nextguardcall := CInt(GetObjProperty(who, "#nextguardcallat"));
	if (ReadGameClock()>nextguardcall) //that is current time is greater than time next guard call allowed at
		SetObjProperty(who, "#nextguardcallat", CInt(ReadGameClock())+3);
		return 1;
	endif
	return 0;
endfunction
