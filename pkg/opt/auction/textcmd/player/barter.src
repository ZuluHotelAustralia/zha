use uo;
use os;

include "include/client";

program textcmd_testgump(who, text)

  var aucmember := GetObjProperty(who, "auction");

  if (aucmember)
     barter(who,text);
  else
     SendSysMessage( who,"You need to join the auction first");
  endif

endprogram


function barter(who, text)

	if (GetObjProperty(who, "barterban"))
		SendSysMessage( who, "You are not allowed to send messages.");
		return;
	endif
	
	var auctioncheck, bartermsgonoff, bartermsg, nextmsg :=CInt(GetObjProperty(who, "#nextbartermsg")), secclock:=ReadGameClock();
	
	if ( secclock< nextmsg)
		SendSysMessage( who, "You have to wait " + (nextmsg-secclock) + " to send another message.");
		return;
	endif
	
	if (!text)
		SendSysMessage( who, "You can type in '.barter on' or '.barter off'");
		bartermsg := RequestInput(who, who.backpack, "Type in your barter message:" );
	else
		bartermsg := text;
	endif
	
	if((bartermsg=="") || (!bartermsg))
		SendSysMessage( who, "Canceled." );
		return;
	endif
	
	if (lower(bartermsg)=="on")
		SendSysMessage( who, "You have turned on barter messages.");
		SetObjProperty(who, "barter", 1);
		return;
	endif
	if (lower(bartermsg)=="off")
		SendSysMessage( who, "You have turned off barter messages.");
		EraseObjProperty(who, "barter");
		return;
	endif
	
	var found := 0;
	foreach mobile in ListMobilesNearLocation(who.x, who.y, who.z, 1)
		if (mobile.npctemplate == "auctioner")
			found := 1;
		endif
	endforeach
	if (!found)
		SendSysMessage( who, "You must be standing next to an auctioner to barter.");
		return;
	endif
	
	foreach chr in EnumerateOnlineCharacters()
		if (GetObjProperty(chr, "auction") && GetObjProperty(chr, "barter"))
			SendSysMessage( chr, who.name + " barters: " + bartermsg, FONT_NORMAL, 400 );
		endif
	endforeach
	
	if (!who.cmdlevel)
		SetObjProperty(who, "#nextbartermsg", ReadGameClock() + 60);
	endif

endfunction