use uo;
use os;

include "include/spelldata";

program ambushtilewalkon_lava( who, tile )

	//done so that auxillary tiles can pass parameters in start_script
	if (TypeOf(who) == "Array")
		tile:= who[2];
		who := who[1];
	endif



	if (who.dead || !who.acct)
		return;
	endif
	
	if (who.cmdlevel >= 1)
		return;
	endif
	
	var mtilestatus := tileStatus(tile);
	if (! mtilestatus[1]||!mtilestatus[2] )
		return;
	endif
	
	if (!HasAmbushTimeElapsed(tile))
		return;
	endif
	SetCurrentAmbushTime(tile, mtilestatus[3]);
	sleep(1);
	PrintTextAbove(tile, "*Ambush!!!*");
	var i:=1;
	var thecritter;
	while (i<=CInt(mtilestatus[2]))
		if( !getobjproperty( tile, "groupspawn" ) )
			thecritter := CreateNpcFromTemplate( mtilestatus[1], CInt(tile.x), CInt(tile.y), CInt(tile.z) );
		else
			thecritter := CreateNpcFromGroup( tile, mtilestatus[1] );
		endif
		if (thecritter)
			var createdxyz := array{};
			createdxyz[1] := CInt(tile.x);
			createdxyz[2] := CInt(tile.y);
			createdxyz[3] := CInt(tile.z);
			SetObjProperty(thecritter,"createdxyz", createdxyz);
			PlayStationaryEffect( thecritter.x, thecritter.y, thecritter.z, FX_SMOKE, 0xa, 0xa );
			PlaySoundEffect( thecritter, 0x280 );
		endif
		i:=i+1;
	endwhile
endprogram

function HasAmbushTimeElapsed(tile)
	//returns true if time elapsed
	var lastwalkedTime := CInt(GetObjProperty(tile, "#lastwalked"));
	if (ReadGameClock()>lastwalkedTime) //that is current time is greater than reserved time
		return 1;
	endif
	return 0;
endfunction

function SetCurrentAmbushTime(tile, timeinterval)
	SetObjProperty(tile, "#lastwalked", ReadGameClock()+CInt(timeinterval));
endfunction

function tileStatus(tile)
//returns an array of file settings for gump above
 	return {GetObjProperty(tile, "createMonsterTemplate"),
	GetObjProperty(tile, "createMonsterTemplateNo"),
	GetObjProperty(tile, "TimeInterval")};
endfunction

function CreateNpcFromGroup( tile, group )

	var cfg := ReadConfigFile( ":spawnpoint:groups" );
	if( !cfg )
		return 0;
	endif

	var elem	:= cfg[group];
	var possibles	:= GetConfigStringArray( elem, "spawn" );
	if( !possibles.size() )
		return 0;
	endif

	var template := possibles[RandomInt(possibles.size())+1];
	if( !template )
		return 0;
	endif
	
	var critter := CreateNpcFromTemplate( template, CInt(tile.x), CInt(tile.y), CInt(tile.z) );

	return critter;

endfunction