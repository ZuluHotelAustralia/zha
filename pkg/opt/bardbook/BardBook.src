use uo;
use os;
use math;
use basic;
use cfgfile;

include "bardbook";

program BardBook( who, book )

	if( (book.container).IsA(POLCLASS_CORPSE) )
		SendSysMessage( who, "You can't use this while it is on the corpse." );
		return 0;
	endif

	if( !ReserveItem(book) )
		return 0;
	endif

	var layout := {
		"page 0",
		"nodispose",
		"gumppic 30 30 2203",
		"text 70  50 33 0",
		"text 230 50 33 1"
	};

	var data := {
		"   Choruses",
		"   Performances"
	};

	var spellnames := {
		"Minstrel's Journey",	// Chorus
		"Companionship",
		"Charm",
		"Sound Wave",
		"Song of Endurance",
		"Song of Recovery",
		"Song of Peace",
		"Disharmony",
		"The Parade",		// Performance
		"The Fortress",
		"The Hero",
		"Mesmerize",
		"Revival",
		"Sound Barrier",
		"Stunning Silence",
		"Chaos"
	};

	var i;
	var y1 := 70;
	var y2 := 70;
	var bits := { 0x01, 0x02, 0x04, 0x08, 0x10, 0x20, 0x40, 0x80 };
	var greater := CInt(GetObjProperty(book,"Greater"));
	var lesser  := CInt(GetObjProperty(book,"Lesser"));

	for( i := 1; i <= 8; i := i + 1 )
		if( lesser & bits[i] )
			layout.append( "button 60 " + y1 + " 2104 2103 1 0 " + i );
			layout.append( "text 80 " + (y1-5) + " 0 " + len(data) );
			data.append( spellnames[i] );
			y1 := y1 + 20;
		endif
	
		if( greater & bits[i] )
			layout.append( "button 220 " + y2 + " 2104 2103 1 0 " + (i+8) );
			layout.append( "text 240 " + (y2-5) + " 0 " + len(data) );
			data.append( spellnames[i+8] );
			y2 := y2 + 20;
		endif
	endfor

	var songid := SendDialogGump( who, layout, data )[0];
	if( !songid )
		return 0;
	endif

	var song := GetScriptBard( songid );

	if( GetObjProperty( book, "CastingOpts" ) )
		SetObjProperty( who, "CastingOpts", GetObjProperty( book, "CastingOpts" ) );
	endif

	SetObjProperty( who, "#Casting", 1 );

	start_script( song, who );
	
	while( GetObjProperty( who, "#Casting" ) )
		sleepms( 500 );
	endwhile

endprogram
