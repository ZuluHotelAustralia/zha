use uo;
use os;


include "include/client";
include "include/areas";
include "include/spelldata";
include "include/speedhackRecallCheck";

const DELAY_BETWEEN_USE	:= 30;

program item_cast_spell( who, item )

	if( !ReserveItem( item ) )
		return;
	endif
	if (! UseOkay(who, item, 500))
		ReleaseItem( item );
		return;
	endif

	//barns - 16/11/03 - code modified to make it more pvp friendly
	PrintTextAbove(who, "*activates recall item*", 3,53);
	PlaySoundEffect( who, 0xff );
	if (! UseOkay(who, item, 2000))
		ReleaseItem( item );
		PlaySoundEffect( who, 0x05d);
		return;
	endif

	PlaySoundEffect( who, SFX_SPELL_RECALL1 );
	
	if (! UseOkay(who, item, 500))
		ReleaseItem( item );
		PlaySoundEffect( who, 0x05d);
		return;
	endif
	SetSpeedCheckNote(who);
	MoveObjectToLocation( who, 1475, 1645, 20 , _DEFAULT_REALM); //AutoConversion
	PlaySoundEffect( who, SFX_SPELL_RECALL2 );
	SetObjProperty( item, "#next_use", CInt(ReadGameClock()+DELAY_BETWEEN_USE) );
	
endprogram

function UseOkay(who, item, delay)
	var myhp := GetHp(who);
	var myx := who.x;
	var myy := who.y;
	sleepms(delay);
	if (!can_cast(who, 0)) // (who, circle 0)
		SendSysMessage(who, "You cannot use that here!");
		return 0;
	endif
	
	// Added by Sno 6/2/03 - to take in effects made by my NoRecallingOut area prop
	
	if (IsForbiddenAreaToRecallFrom(who))
		SendSysMessage(who, "A magical barrier keeps you from recalling.");
		return 0;
	endif

	var next_use := CInt(GetObjProperty( item, "#next_use" ));
	if( ReadGameClock() < CInt(next_use) )
		SendSysMessage( who, "You must wait before using it again." );
		return 0;
	endif

	if( !GetObjProperty( item, "RequireNoCharges" ) )
		var charges := CInt(GetObjProperty( item, "charges" ));
		if( !charges )
			PrintTextAbovePrivate( item, "That is out of charges", who );
			return 0;
		else
			charges := charges - 1;
			SetObjProperty( item, "charges", charges );
		endif
	endif
	
	if (! (myhp<=GetHp(who)&&myx==who.x&&myy==who.y))
		SendSysMessage(who, "Recall interrupted!");
		return 0;
	endif
	return 1;
endfunction