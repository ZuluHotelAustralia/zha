use uo;
use os;
include "include/gumps";
include "include/random";
include "include/client";
//include "arcanesecrets";

var nine:="9) Unknown (? + ?)";
var ten:="10) Unknown (? + ?)";
var eleven:="11) Unknown (? + ?)";
var twelve:="12) Unknown (? + ?)";
var thirteen:="13) Unknown (? + ?)";
var fourteen:="14) Unknown (? + ?)";


program arcanetome (user, tome)

	if (!ReserveItem(tome))
		return;
	endif
	if (tome.container != user.backpack )
		SendSysMessage(user, "The tome must be in your backpack");
		return;
	endif
	if (!GetObjProperty(user, "IsMage"))
		SendSysMessage(user, "The tome is beyond your comprehension");
		return;
	endif

	GFInitGump();
	GFPage(0);
	GFResizePic(0, 0, BKGND_PAPER_EX, 200, 250);
	GFTextMid(20, 20, 155, 18, "The Tome");
	GFTextMid(20, 40, 155, 18, "of");
	GFTextMid(20, 60, 155, 18, "Arcane Secrets");
	GFTextLine(35, 120, 0, "Read the Tome");
	GFTextLine(35, 140, 0, "Mix Components");

	var button1  := GFButtonID( 20, 125, G_BLUE_SMALL_CIRCLE_0, G_BLUE_SMALL_CIRCLE_1, 1);
	var button2  := GFButtonID( 20, 145, G_BLUE_SMALL_CIRCLE_0, G_BLUE_SMALL_CIRCLE_1, 1);

	var result := GFSendGump(user);

	if (result[button1.keyid])
		ReadTomePageOne(user, tome);
	elseif (result[button2.keyid])
		MixComponents(user, tome);
	endif

endprogram

function ReadTomePageOne(user, tome)

	GFInitGump();
	GFPage(0);
	GFResizePic(0, 0, BKGND_PAPER_EX, 410, 500);
	GFTextMid(20, 20, 350, 118, "THE TOME OF ARCANE SECRETS");
	GFTextLine(20, 50, 0, "This tome is difficult to decipher for it is written");
	GFTextLine(20, 70, 0, "in an arcane language. Fortunately, it is strewn");
	GFTextLine(20, 90, 0, "with diagrams and you are able to glean some");
	GFTextLine(20, 110, 0, "information from it.");
	GFTextLine(20, 130, 0, "It seems to be a set of instructions for creating");
	GFTextLine(20, 150, 0, "a Scroll of Arcane Power. You are unable to fathom");
	GFTextLine(20, 170, 0, "the complex procedures, but they appear to involve");
	GFTextLine(20, 190, 0, "combining certain rare ingredients. Some procedures");
	GFTextLine(20, 210, 0, "involve Alchemy and some involve Inscription. You");
	GFTextLine(20, 230, 0, "compile a list of the base ingredients:");
	GFTextLine(30, 270, 18, "1) The Blackened Heart of a Fallen Paladin");
	GFTextLine(30, 290, 18, "2) The Foul Brain of a Daemon of Death");
	GFTextLine(30, 310, 18, "3) The Charred Tooth of a Flaming Wyrm");
	GFTextLine(30, 330, 18, "4) An Empty Page from a Hellborne Tome");
	GFTextLine(30, 350, 18, "5) A Venom Sac from a Spider Goddess");
	GFTextLine(30, 370, 18, "6) The Radiant Core of a Rainbow Spirit");
	GFTextLine(30, 390, 18, "7) The Silken Threads from an Arachnid Queen");
	GFTextLine(30, 410, 18, "8) The Blood of a Vampire Lord");
	GFTextLine(290, 450, 0, "Next Page");
	var button1  := GFButtonID( 370, 450, G_GREEN_ARROW_RIGHT_0, G_GREEN_ARROW_RIGHT_1, 1);	
	

	var result:=GFSendGump(user);
	
	if (result[button1.keyid])
		ReadTomePageTwo(user, tome);
	endif
	return;
endfunction

function ReadTomePageTwo(user, tome)

	if (GetObjProperty(tome, "nine"))
		nine:=GetObjProperty(tome, "nine");
	endif
	if (GetObjProperty(tome, "ten"))
		ten:=GetObjProperty(tome, "ten");
	endif
	if (GetObjProperty(tome, "eleven"))
		eleven:=GetObjProperty(tome, "eleven");
	endif
	if (GetObjProperty(tome, "twelve"))
		twelve:=GetObjProperty(tome, "twelve");
	endif
	if (GetObjProperty(tome, "thirteen"))
		thirteen:=GetObjProperty(tome, "thirteen");
	endif
	if (GetObjProperty(tome, "fourteen"))
		fourteen:=GetObjProperty(tome, "fourteen");
	endif

	GFInitGump();
	GFPage(0);
	GFResizePic(0, 0, BKGND_PAPER_EX, 410, 500);
	GFTextMid(20, 20, 350, 118, "THE TOME OF ARCANE SECRETS");
	GFTextLine(20, 50, 0, "Somehow, you must match each ingredient to another");
	GFTextLine(20, 70, 0, "ingredient to create 4 new components:");
	GFTextLine(30, 110, 18, nine);
	GFTextLine(30, 130, 18, ten);
	GFTextLine(30, 150, 18, eleven);
	GFTextLine(30, 170, 18, twelve);
	GFTextLine(20, 210, 0, "Once again, these components are combined to form");
	GFTextLine(20, 230, 0, "another 2 components:");
	GFTextLine(30, 270, 18, thirteen);
	GFTextLine(30, 290, 18, fourteen);
	GFTextLine(20, 330, 0, "Once these components are created, you can finally");
	GFTextLine(20, 350, 0, "make the Scroll of Arcane Power!");
	GFTextLine(30, 390, 18, "15) Scroll of Arcane Power (13 + 14)");
	GFTextLine(55, 450, 0, "Prev Page");
	var button1  := GFButtonID( 30, 450, G_RED_ARROW_LEFT_0, G_RED_ARROW_LEFT_1, 1);	
	

	var result:=GFSendGump(user);
	
	if (result[button1.keyid])
		ReadTomePageOne(user, tome);
	endif
	return;
endfunction

function MixComponents(user, tome)
	
	SendSysMessage(user, "Target the first component");
	var firstcomp := Target(user);
	if (!firstcomp)
		SendSysMessage(user, "Cancelled");
		return;
	endif
	if (firstcomp.container != user.backpack)
		SendSysMessage(user, "That must be in your backpack");
		return;
	endif
	var firstcompnum := GetObjProperty(firstcomp, "Ingredient");
	if (!firstcompnum || GetObjProperty(firstcomp, "mixing"))
		SendSysMessage(user, "You can't mix that!");
		return;
	endif
	SetObjProperty(firstcomp, "mixing", 1);
	firstcomp.movable := 0;
	SendSysMessage(user, "Target the second component");
	var secondcomp := Target(user);
	if (!secondcomp)
		SendSysMessage(user, "Cancelled");
		EraseObjProperty(firstcomp, "mixing");
		firstcomp.movable := 1;
		return;
	endif
	if (secondcomp.container != user.backpack)
		SendSysMessage(user, "That must be in your backpack");
		EraseObjProperty(firstcomp, "mixing");
		firstcomp.movable := 1;
		return;
	endif
	var secondcompnum := GetObjProperty(secondcomp, "Ingredient");
	if (!secondcompnum || GetObjProperty(secondcomp, "mixing"))
		SendSysMessage(user, "You can't mix that!");
		EraseObjProperty(firstcomp, "mixing");
		firstcomp.movable := 1;
		return;
	endif
	SetObjProperty(secondcomp, "mixing", 1);
	secondcomp.movable := 0;
	
	var skillid;
	case (firstcompnum)
		1:
		2:
		3:	skillid := SKILLID_ALCHEMY;
		4:	skillid := SKILLID_INSCRIPTION;
		5:	
		6:	skillid := SKILLID_ALCHEMY;
		7:	skillid := SKILLID_INSCRIPTION;
		8:	
		9:	skillid := SKILLID_ALCHEMY;
		10:	
		11:	skillid := SKILLID_INSCRIPTION;
		12:	skillid := SKILLID_ALCHEMY;
		13:
		14:	skillid := SKILLID_INSCRIPTION;
	endcase
		
	SendSysMessage(user, "You attempt to combine the components...");
	var i;
	for (i:=0; i<Random(3)+3; i:=i+1)
		PerformAction(user, ANIM_SALUTE);
		if (skillid == SKILLID_ALCHEMY)
			PlaySoundEffect(user, SFX_243);
		else
			PlaySoundEffect(user, SFX_24A);
		endif
		sleepms(1500);
	endfor
	
	if (GetObjProperty(firstcomp, "Match") != secondcompnum)
		SendSysMessage(user, "The objects are incompatible! They're going to blow!");
		BlowUp(user, tome, firstcomp, secondcomp);
		return;
	endif
	
	var difficulty;
	if (firstcompnum<9)
		difficulty:= 140;
	elseif (firstcompnum<13)
		difficulty:= 145;
	else
		difficulty:=150;
	endif
		
	if (!CheckSkill(user, skillid, difficulty, 0))
		SendSysMessage(user, "The objects are compatible, but you fail the procedure! They're going to blow!");
		BlowUp(user, tome, firstcomp, secondcomp);
		return;
	endif
	var newcomponent:= CreateItemInBackpack(user, GetObjProperty(firstcomp, "Result"));
	if(!newcomponent)
		SendSysMessage(user, "Something went wrong....");
		SubtractAmount(firstcomp, 1);
		SubtractAmount(secondcomp, 1);
		return;
	endif

	SendSysMessage(user, "Success!");
	
	var update, updatetext, successtext;
	case(GetObjProperty(newcomponent, "Ingredient"))
		9:	update:="nine";
			updatetext:="9) Infernal Dragontooth (2 + 3)";
			successtext:="The acidic brain fluids hollow out the wyrm's tooth!";
		10:	update:="ten";
			updatetext:="10) Purified Paladin's Heart (1 + 6)";
			successtext:="The soul gem purifies the heart of the fallen paladin!";
		11:	update:="eleven";
			updatetext:="11) Enchanted Codex Scroll (4 + 7)";
			successtext:="The webs form spidery letters on the codex page!";
		12:	update:="twelve";
			updatetext:="12) Envenomed Vampire Blood (5 + 8)";
			successtext:="The blood and venom coalesce into a steaming brew!";
		13:	update:="thirteen";
			updatetext:="13) Infernal Dragontooth Pen (9 + 12)";
			successtext:="You fill the hollowed tooth with the venomous blood!";
		14:	update:="fourteen";
			updatetext:="14) Sanctified Codex Scroll (10 + 11)";
			successtext:="The blood from the redeemed paladin's heart sanctifies the scroll!";
		15:	successtext:="Using the dragontooth pen, you inscribe a Scroll of Arcane Power on the sanctified codex scroll!";
			update:="destroy";
	endcase
	SendSysMessage(user, successtext);
	SubtractAmount(firstcomp, 1);
	SubtractAmount(secondcomp, 1);
	PlaySoundEffect(user, 0x104);
	if (!GetObjProperty(tome, update))
		if (GetObjProperty(newcomponent, "Ingredient") == 15)
			SendSysMessage(user, "The tome slowly fades away....");
			SubtractAmount(tome, 1);
		else
			SendSysMessage(user, "You scribble some notes into your tome");	
			SetObjProperty(tome, update, updatetext);
		endif
	endif
endfunction

function BlowUp(user, tome, first, second)

	PlaySoundEffect(user, 0x103);
	Detach();
	sleepms(2500);
	SubtractAmount(first, 1);
	SubtractAmount(second, 1);	
	PlayObjectCenteredEffect (user, 0x36b0, 1, 0);
	ApplyDamage(user, Random(100)+200);
	return;
	
endfunction















