use uo;
use os;

include "include/hitscriptinc";
include "include/attributes";
include "include/damages";
include "include/client";


program lifedrainscript(attacker, defender, weapon, armor, basedamage, rawdamage)

	if (TypeOf(attacker) == "Array")
	//required for archery sys hook handling
		var temparr := attacker;	
		attacker := temparr[1];
		defender := temparr[2];
		weapon := temparr[3];
		armor := temparr[4];
		basedamage := temparr[5];
		rawdamage := temparr[6];
	endif


	var drained;
	var drainer;
	
	rawdamage := RecalcDmg( attacker, defender, weapon, armor, basedamage );

	if( GetObjProperty( weapon, "Cursed" ) )
		drained := attacker;
		drainer := defender;
	else
		drained := defender;
		drainer := attacker;
	endif

	var amount := rawdamage / 2;
	var clevel := CInt(GetObjProperty(attacker, CLASSEID_DEATHKNIGHT));
	if (clevel)
		if (GetObjProperty(GetEquipmentByLayer( attacker, LAYER_MOUNT ), "specialsteed")) //must be mounted on specialsteed
			amount := CInt(amount + (DEATHKNIGHT_SPECIAL_WEAPON_BONUS_EFFECTIVENESS*clevel));
		endif
	endif

	ApplyTheDamage( drained, attacker, amount );
	SetHp(drainer, GetHp(drainer) + amount);
	if( GetHp(drainer) > GetMaxHp(drainer))
		SetHp(drainer, GetMaxHp(drainer));
	endif

	DealDamage( attacker, defender, weapon, armor, basedamage, rawdamage );

endprogram
