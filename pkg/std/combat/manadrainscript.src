use uo;
use os;

include "include/hitscriptinc";
include "include/attributes";
include "include/client";


program manadrainscript(attacker, defender, weapon, armor, basedamage, rawdamage)

	if (TypeOf(attacker) == "Array")
	//required for archery sys hook handling
		var temparr := attacker;	
		attacker := temparr[1];
		defender := temparr[2];
		weapon := temparr[3];
		armor := temparr[4];
		basedamage := temparr[5];
		rawdamage := temparr[6];
	endif


	var drained;
	var drainer;
	
	rawdamage := RecalcDmg( attacker, defender, weapon, armor, basedamage );

	if( GetObjProperty( weapon, "Cursed" ) )
		drained := attacker;
		drainer := defender;
	else
		drained := defender;
		drainer := attacker;
	endif

	var amount := rawdamage / 2;
	var clevel := Cint(GetObjProperty(attacker, CLASSEID_DEATHKNIGHT));
	if (clevel>=4)
		if (GetObjProperty(GetEquipmentByLayer( attacker, LAYER_MOUNT ), "specialsteed")) //must be mounted on specialsteed
			amount := CInt(amount + (DEATHKNIGHT_SPECIAL_WEAPON_BONUS_EFFECTIVENESS*clevel));
		endif
	endif

	if( amount > GetMana(drained))
		SetMana(drained, 0);
	else
		SetMana(drained, GetMana(drained) - amount);
	endif

	SetMana(drainer, GetMana(drainer) + amount);
	if( GetMana(drainer) > GetMaxMana(drainer))
		SetMana(drainer, drainer.maxmana);
	endif

	DealDamage( attacker, defender, weapon, armor, basedamage, rawdamage );

endprogram
