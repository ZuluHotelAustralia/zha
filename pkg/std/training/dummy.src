use uo;
use os;

include "include/client";
include "include/attributes";
include "include/objtype";
include "include/utility";

var cfg := ReadConfigFile(":combat:itemdesc");

program training_dummy(who, dummy)
  EraseObjProperty(who, "IsMeditating");
  if(Distance(who, dummy) > 1)
    SendSysMessage(who, "You must be standing next to that to use it.");
    return;
  endif
  var attributeid := lower(cfg[who.weapon.objtype].Attribute);
  if(!attributeid)
	attributeid := ATTRIBUTEID_WRESTLING;
  endif
  if(GetObjProperty(dummy, "#swinging"))
    SendSysMessage(who, "That dummy is already in use");
    return;
  endif
  case(attributeid) // For some reason attributes on weaps are "swords" instead of "swordsmanship"
    "mace":
	CheckSwing(who, SKILLID_MACEFIGHTING, ANIM_ATTACK_1HAND_DOWN, dummy);
    "fencing":
	CheckSwing(who, SKILLID_FENCING, ANIM_ATTACK_1HAND_JAB, dummy);
    "swords":
	CheckSwing(who, SKILLID_SWORDSMANSHIP, ANIM_ATTACK_1HAND_WIDE, dummy);
    ATTRIBUTEID_WRESTLING:
	CheckSwing(who, SKILLID_WRESTLING, 0x9, dummy);
  endcase
endprogram

function CheckSwing(who, skillid, action, dummy)
  PerformAction(who, action);
  sleepms(500);
  Detach();
  if(GetEffectiveSkill(who, skillid) >= 25.0)
    SendSysMessage(who, "You cannot learn any more from simply hitting a dummy.");
  else
    CheckSkill(who, skillid, 10, 40);
    CheckSkill(who, SKILLID_TACTICS, 10, 20);
  endif
  PlaySoundEffect(who, SFX_13C);
  SetObjProperty(dummy, "#swinging", 1);
  if(dummy.objtype == UOBJ_DUMMYFREEZE_NS)
    dummy.graphic := UOBJ_DUMMYSWING_NS;
    sleepms(3600);
    dummy.graphic := UOBJ_DUMMYFREEZE_NS;
  elseif(dummy.objtype == UOBJ_DUMMYFREEZE_EW)
    dummy.graphic := UOBJ_DUMMYSWING_EW;
    sleepms(3600);
    dummy.graphic := UOBJ_DUMMYFREEZE_EW;
  endif
  EraseObjProperty(dummy, "#swinging");
endfunction
