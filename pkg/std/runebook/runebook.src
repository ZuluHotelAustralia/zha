use uo;
use os;
use math;
use basic;

include "customspells";
include "include/gumps";
include "include/client";
include "include/objtype";

const RENAME_BOOK		:= 0x01;
const RECALL_WITH_CHARGES	:= 0x10;
const RECALL_WITHOUT_CHARGES	:= 0x20;
const GATE_WITHOUT_CHARGES	:= 0x30;
const SET_DEFAULT_LOCATION	:= 0x40;
const RESET_DEFAULT_LOCATION	:= 0x50;
const DROP_RUNE			:= 0x60;


program open_runebook( who, runebook )

	SetScriptController(who);

	if( !Accessible( who, runebook ) )
		SendSysMessage( who, "You can't reach that." );
		return 0;
	endif

	if( !ReserveItem( runebook ) )
		return 0;
	endif

	if(who.hidden)
		SendSysMessage(who, "You cannot recall while hidden");
		return;
	endif

	var x;
	var y;
	var msg;
	var ret;
	var info;
	var name;
	var type;
	var index;
	var rune_count;
	var page_count;
	var defloc	 := 0;
	var charges	 := CInt( GetObjProperty( runebook, "charges" ) );
	var maxcharges := CInt( GetObjProperty( runebook, "maxcharges" ) );
	if( !maxcharges )
		maxcharges := 5;
	endif

	var runes_def	 := GetObjProperty( runebook, "RuneDefs" );
	if( !runes_def )
		runes_def := array;
	endif

	var max_page	 := CInt( (runes_def.size()+1) / 2 );

	var layout := { "page 0",
			  "nodispose",
			  "gumppic 30   5 " + CInt(G_BOOK_SMALL_1),
			  "gumppic 55  45 " + CInt(G_DECO_LINE_LEFT),
			  "gumppic 75  45 " + CInt(G_DECO_LINE_CENTER),
			  "gumppic 90  45 " + CInt(G_DECO_LINE_CENTER),
			  "gumppic 105 45 " + CInt(G_DECO_LINE_CENTER),
			  "gumppic 120 45 " + CInt(G_DECO_LINE_CENTER),
			  "gumppic 135 45 " + CInt(G_DECO_LINE_CENTER),
			  "gumppic 150 45 " + CInt(G_DECO_LINE_CENTER),
			  "gumppic 165 45 " + CInt(G_DECO_LINE_RIGHT),
			  "gumppic 218 45 " + CInt(G_DECO_LINE_LEFT),
			  "gumppic 233 45 " + CInt(G_DECO_LINE_CENTER),
			  "gumppic 248 45 " + CInt(G_DECO_LINE_CENTER),
			  "gumppic 263 45 " + CInt(G_DECO_LINE_CENTER),
			  "gumppic 278 45 " + CInt(G_DECO_LINE_CENTER),
			  "gumppic 293 45 " + CInt(G_DECO_LINE_CENTER),
			  "gumppic 308 45 " + CInt(G_DECO_LINE_CENTER),
			  "gumppic 323 45 " + CInt(G_DECO_LINE_RIGHT),

			  "text 90  35 0 0",
			  "text 228 35 0 1"};


	var data := { "Charges: " + charges,		// 0 Charges
			"Max Charges:" + maxcharges,	// 1 Max Charges
			"Rename Book",			// 2 Rename Book
			"Set default",			// 3 Set default
			"Drop rune" };			// 4 Drop rune


	case( max_page )
		0:	break;

		1:	layout.append( "button 60  182 " + CInt(G_SPELLCIRCLE_1) + " " + CInt(G_SPELLCIRCLE_1) + " 0 2" );

		2:	layout.append( "button 60  182 " + CInt(G_SPELLCIRCLE_1) + " " + CInt(G_SPELLCIRCLE_1) + " 0 2" );
			layout.append( "button 95  182 " + CInt(G_SPELLCIRCLE_2) + " " + CInt(G_SPELLCIRCLE_2) + " 0 3" );

		3:	layout.append( "button 60  182 " + CInt(G_SPELLCIRCLE_1) + " " + CInt(G_SPELLCIRCLE_1) + " 0 2" );
			layout.append( "button 95  182 " + CInt(G_SPELLCIRCLE_2) + " " + CInt(G_SPELLCIRCLE_2) + " 0 3" );
			layout.append( "button 130 182 " + CInt(G_SPELLCIRCLE_3) + " " + CInt(G_SPELLCIRCLE_3) + " 0 4" );

		4:	layout.append( "button 60  182 " + CInt(G_SPELLCIRCLE_1) + " " + CInt(G_SPELLCIRCLE_1) + " 0 2" );
			layout.append( "button 95  182 " + CInt(G_SPELLCIRCLE_2) + " " + CInt(G_SPELLCIRCLE_2) + " 0 3" );
			layout.append( "button 130 182 " + CInt(G_SPELLCIRCLE_3) + " " + CInt(G_SPELLCIRCLE_3) + " 0 4" );
			layout.append( "button 165 182 " + CInt(G_SPELLCIRCLE_4) + " " + CInt(G_SPELLCIRCLE_4) + " 0 5" );

		5:	layout.append( "button 60  182 " + CInt(G_SPELLCIRCLE_1) + " " + CInt(G_SPELLCIRCLE_1) + " 0 2" );
			layout.append( "button 95  182 " + CInt(G_SPELLCIRCLE_2) + " " + CInt(G_SPELLCIRCLE_2) + " 0 3" );
			layout.append( "button 130 182 " + CInt(G_SPELLCIRCLE_3) + " " + CInt(G_SPELLCIRCLE_3) + " 0 4" );
			layout.append( "button 165 182 " + CInt(G_SPELLCIRCLE_4) + " " + CInt(G_SPELLCIRCLE_4) + " 0 5" );
			layout.append( "button 230 182 " + CInt(G_SPELLCIRCLE_5) + " " + CInt(G_SPELLCIRCLE_5) + " 0 6" );

		6:	layout.append( "button 60  182 " + CInt(G_SPELLCIRCLE_1) + " " + CInt(G_SPELLCIRCLE_1) + " 0 2" );
			layout.append( "button 95  182 " + CInt(G_SPELLCIRCLE_2) + " " + CInt(G_SPELLCIRCLE_2) + " 0 3" );
			layout.append( "button 130 182 " + CInt(G_SPELLCIRCLE_3) + " " + CInt(G_SPELLCIRCLE_3) + " 0 4" );
			layout.append( "button 165 182 " + CInt(G_SPELLCIRCLE_4) + " " + CInt(G_SPELLCIRCLE_4) + " 0 5" );
			layout.append( "button 230 182 " + CInt(G_SPELLCIRCLE_5) + " " + CInt(G_SPELLCIRCLE_5) + " 0 6" );
			layout.append( "button 265 182 " + CInt(G_SPELLCIRCLE_6) + " " + CInt(G_SPELLCIRCLE_6) + " 0 7" );

		7:	layout.append( "button 60  182 " + CInt(G_SPELLCIRCLE_1) + " " + CInt(G_SPELLCIRCLE_1) + " 0 2" );
			layout.append( "button 95  182 " + CInt(G_SPELLCIRCLE_2) + " " + CInt(G_SPELLCIRCLE_2) + " 0 3" );
			layout.append( "button 130 182 " + CInt(G_SPELLCIRCLE_3) + " " + CInt(G_SPELLCIRCLE_3) + " 0 4" );
			layout.append( "button 165 182 " + CInt(G_SPELLCIRCLE_4) + " " + CInt(G_SPELLCIRCLE_4) + " 0 5" );
			layout.append( "button 230 182 " + CInt(G_SPELLCIRCLE_5) + " " + CInt(G_SPELLCIRCLE_5) + " 0 6" );
			layout.append( "button 265 182 " + CInt(G_SPELLCIRCLE_6) + " " + CInt(G_SPELLCIRCLE_6) + " 0 7" );
			layout.append( "button 300 182 " + CInt(G_SPELLCIRCLE_7) + " " + CInt(G_SPELLCIRCLE_7) + " 0 8" );

		8:	layout.append( "button 60  182 " + CInt(G_SPELLCIRCLE_1) + " " + CInt(G_SPELLCIRCLE_1) + " 0 2" );
			layout.append( "button 95  182 " + CInt(G_SPELLCIRCLE_2) + " " + CInt(G_SPELLCIRCLE_2) + " 0 3" );
			layout.append( "button 130 182 " + CInt(G_SPELLCIRCLE_3) + " " + CInt(G_SPELLCIRCLE_3) + " 0 4" );
			layout.append( "button 165 182 " + CInt(G_SPELLCIRCLE_4) + " " + CInt(G_SPELLCIRCLE_4) + " 0 5" );
			layout.append( "button 230 182 " + CInt(G_SPELLCIRCLE_5) + " " + CInt(G_SPELLCIRCLE_5) + " 0 6" );
			layout.append( "button 265 182 " + CInt(G_SPELLCIRCLE_6) + " " + CInt(G_SPELLCIRCLE_6) + " 0 7" );
			layout.append( "button 300 182 " + CInt(G_SPELLCIRCLE_7) + " " + CInt(G_SPELLCIRCLE_7) + " 0 8" );
			layout.append( "button 335 182 " + CInt(G_SPELLCIRCLE_8) + " " + CInt(G_SPELLCIRCLE_8) + " 0 9" );
	endcase

	// Main Page
	layout.append( "page 1" );
	layout.append( "text 90 17 0 2" );
	layout.append( "button 60  15 " + CInt(G_RED_DIAMOND_0) + " " + CInt(G_RED_DIAMOND_1) + " 1 0 " + CInt(RENAME_BOOK) );

	if( runes_def.size() )
		layout.append( "button 324 9 " + CInt(G_CORNER_RIGHT)  + " " + CInt(G_CORNER_RIGHT)  + " 0 2" );
	endif

	x := 60;
	y := 56;
	for( rune_count := 1; rune_count <= runes_def.size(); rune_count := rune_count + 1 )
		if( runes_def[rune_count] )
			runes_def[rune_count].dtindex	:= data.size();

			if( runes_def[rune_count].defloc )
				if( !defloc )
					defloc := rune_count;
				else
					SetDefaultLocation( runebook, runes_def, defloc, rune_count );
				endif
			endif

			if( rune_count == 9 )
				x := 219;
				y := 56;
			endif

			name := runes_def[rune_count].name;
			if( name["A recall rune for "] )
				name := name - "A recall rune for ";
				runes_def[rune_count].name := name;
			elseif( name["A recall rune for"] )
				name := name - "A recall rune for";
				runes_def[rune_count].name := name;
			endif

			if( !name )
				name := "a marked recall rune";
				runes_def[rune_count].name := name;

			else //if( len(name) > 15 ) //<---- speed UP
				name := name[1,15]; //TruncName( name ); //<---- speed UP
			endif

			data.append( name );
			layout.append( "text " + (x+15) + " " + y + " 0 " + runes_def[rune_count].dtindex );
			layout.append( "button " + x + " " + (y+5) + " " + CInt(G_BLUE_SMALL_CIRCLE_1) + " " + CInt(G_BLUE_SMALL_CIRCLE_0) + " 1 0 " + CInt(RECALL_WITH_CHARGES+rune_count-1) );
			y := y + 15;
		else
			break;
		endif
	endfor

	// Other pages
	rune_count := 1;
	for( page_count := 2; page_count <= (max_page+1); page_count := page_count+1 )
		layout.append( "page " + page_count );
		layout.append( "button 53  9 " + CInt(G_CORNER_LEFT) + " " + CInt(G_CORNER_LEFT) + " 0 " + CInt(page_count-1) );

		if( page_count < (max_page+1) )
			layout.append( "button 325 9 " + CInt(G_CORNER_RIGHT) + " " + CInt(G_CORNER_RIGHT) + " 0 " + CInt(page_count+1) );
		endif

		if( rune_count <= runes_def.size() )
			info := runes_def[rune_count];
			if( info )
				if( info.defloc )
					layout.append( "button 90 15 " + CInt(G_SMALL_RED_CIRCLE) + " " + CInt(G_SMALL_RED_CIRCLE) + " 1 0 " + CInt(RESET_DEFAULT_LOCATION+rune_count-1) );
				else
					layout.append( "button 90 15 " + CInt(G_SMALL_GREEN_CIRCLE) + " " + CInt(G_SMALL_GREEN_CIRCLE) + " 1 0 " + CInt(SET_DEFAULT_LOCATION+rune_count-1) );
				endif
//<---- speed UP
/*				layout.append( "text 65 76 0 " + data.size() );
				data.append( GetSextantLatitude( info.toy ) );
				layout.append( "text 65 91 0 " + data.size() );
				data.append( GetSextantLongitude( info.tox ) );
*/
				layout.append( "text 105  10 0 3" );
				layout.append( "text 80  110 0 4" );
				layout.append( "text 75   56 0 " + info.dtindex );
				layout.append( "button 60  61  " + CInt(G_BLUE_SMALL_CIRCLE_1)   + " " + CInt(G_BLUE_SMALL_CIRCLE_0)   + " 1 0 " + CInt(RECALL_WITH_CHARGES+rune_count-1) );
				layout.append( "button 65  115 " + CInt(G_GOLD_ARROW_DOWN_0)     + " " + CInt(G_GOLD_ARROW_DOWN_1)     + " 1 0 " + CInt(DROP_RUNE+rune_count-1) );
				layout.append( "button 65  135 " + CInt(G_SPELLICON_RECALL)      + " " + CInt(G_SPELLICON_RECALL)      + " 1 0 " + CInt(RECALL_WITHOUT_CHARGES+rune_count-1) );
				layout.append( "button 135 135 " + CInt(G_SPELLICON_GATE_TRAVEL) + " " + CInt(G_SPELLICON_GATE_TRAVEL) + " 1 0 " + CInt(GATE_WITHOUT_CHARGES+rune_count-1) );
				rune_count := rune_count + 1;
			endif
		else
			break;
		endif

		if( rune_count <= runes_def.size() )
			info	   := runes_def[rune_count];
			if( info )
				if( info.defloc )
					layout.append( "button 230 15 " + CInt(G_SMALL_RED_CIRCLE) + " " + CInt(G_SMALL_RED_CIRCLE) + " 1 0 " + CInt(RESET_DEFAULT_LOCATION+rune_count-1) );
				else
					layout.append( "button 230 15 " + CInt(G_SMALL_GREEN_CIRCLE) + " " + CInt(G_SMALL_GREEN_CIRCLE) + " 1 0 " + CInt(SET_DEFAULT_LOCATION+rune_count-1) );
				endif
//<---- speed UP
/*				layout.append( "text 225 76 0 " + data.size() );
				data.append( GetSextantLatitude( info.toy ) );
				layout.append( "text 225 91 0 " + data.size() );
				data.append( GetSextantLongitude( info.tox ) );
*/
				layout.append( "text 245  10 0 3" );
				layout.append( "text 240 110 0 4" );
				layout.append( "text 234  56 0 " + info.dtindex );
				layout.append( "button 219 61  " + CInt(G_BLUE_SMALL_CIRCLE_1)   + " " + CInt(G_BLUE_SMALL_CIRCLE_0)   + " 1 0 " + CInt(RECALL_WITH_CHARGES+rune_count-1) );
				layout.append( "button 225 115 " + CInt(G_GOLD_ARROW_DOWN_0)     + " " +CInt(G_GOLD_ARROW_DOWN_1)     + " 1 0 " + CInt(DROP_RUNE+rune_count-1) );
				layout.append( "button 225 135 " + CInt(G_SPELLICON_RECALL)      + " " +CInt(G_SPELLICON_RECALL)      + " 1 0 " + CInt(RECALL_WITHOUT_CHARGES+rune_count-1) );
				layout.append( "button 295 135 " + CInt(G_SPELLICON_GATE_TRAVEL) + " " +CInt(G_SPELLICON_GATE_TRAVEL) + " 1 0 " + CInt(GATE_WITHOUT_CHARGES+rune_count-1) );
				rune_count := rune_count + 1;
			endif
		else
			break;
		endif
	endfor

	ret := SendDialogGump( who, layout, data );
	if( !ret[0] )
		return;
	endif

	ret   := CInt( ret[0] );
	type  := CInt( ret/0x10 );
	index := CInt(ret - CInt((type*0x10))+1);

	case( type )
		0:	if( RenameBook( who, runebook ) )
			msg := "The runebook was successfuly renamed to " + CStr(GetObjProperty(runebook, "name" ) );
			endif
			break;

		1:	if( !RecallWithCharges( who, runebook, charges, runes_def, index ) )
				SendSysMessage( who, "Recall Failed." );
			endif
			break;

		2:	if( !RecallWithoutCharges( who, runebook, runes_def, index ) )
				SendSysMessage( who, "Recall Failed." );
			endif
			break;

		3:	ReleaseItem( runebook );
			if( !GateWithoutCharges( who, runebook, runes_def, index ) )
				SendSysMessage( who, "Gate Failed." );
			endif
			break;

		4:	if( SetDefaultLocation( runebook, runes_def, index, defloc ) )
				msg := "Rune " + runes_def[index].name + " was successfuly set as default location.";
			endif
			break;

		5:	if( ResetDefaultLocation( runebook, runes_def, index ) )
				msg := "Default local " + runes_def[index].name + " was successfuly reseted.";
			endif
			break;

		6:	if( !DropRune( who, runebook, runes_def, index ) )
				msg := "Couldn't drop the rune.";
			endif
			break;
	endcase

	SetObjProperty( runebook, "RuneDefs", runes_def );

	if( msg )
		SendSysMessage( who, msg );
	endif

	UpdateName( runebook, charges );

endprogram


function UpdateName( runebook, charges )

	set_critical(1);

	var name := GetObjProperty( runebook, "name" );
	if( !name )
		name := "Runebook";
	endif

	runebook.name := name + " [charges: " + charges + "]";

	set_critical(0);

endfunction


function RenameBook( who, runebook )

	var name := SendTextEntryGump( who, "Enter the new name of the runebook. ", TE_CANCEL_ENABLE, TE_STYLE_NORMAL );
	if( name && NameValidation(name))
		SetObjProperty( runebook, "name", name );
		return 1;
	else
		return 0;
	endif

endfunction


function RecallWithCharges( who, runebook, byref charges, byref runes_def, index )

	if( !runes_def[index] )
		SendSysMessage( who, "Error 1: Rune not found." );
		return 0;
	endif

	var new_charges := CInt( GetObjProperty( runebook, "charges" ) );

	if( new_charges < 1 )
		SendSysMessage( who, "The runebook is out of charges.");
		SetObjProperty( runebook, "charges", 0 );
	else
		var tox := CInt(runes_def[index].tox);
		var toy := CInt(runes_def[index].toy);
		var toz := CInt(runes_def[index].toz);

		if( !tox && !toy && !toz )
			SendSysMessage( who, "Error 2: Blank rune!" );
			DestroyRune( runebook, runes_def, index );
			return 0;
		endif

		new_charges := new_charges - 1;
		SetObjProperty( runebook, "charges", new_charges );
		charges	:= new_charges;

		if( !CustomRecall( who, tox, toy, toz, 1 ) )
			return 0;
		endif
	endif

	return 1;

endfunction


function RecallWithoutCharges( who, runebook, byref runes_def, index )

	if( !runes_def[index] )
		SendSysMessage( who, "Error 1: Rune not found." );
		return 0;
	endif

	var tox := CInt(runes_def[index].tox);
	var toy := CInt(runes_def[index].toy);
	var toz := CInt(runes_def[index].toz);

	if( !tox && !toy && !toz )
		SendSysMessage( who, "Error 2: Blank rune!" );
		DestroyRune( runebook, runes_def, index );
		return 0;
	endif

	if( !CustomRecall( who, tox, toy, toz ) )
		return 0;
	endif

	return 1;

endfunction


function GateWithoutCharges( who, runebook, byref runes_def, index )

	if( !runes_def[index] )
		SendSysMessage( who, "Error 1: Rune not found." );
		return 0;
	endif

	var tox := CInt(runes_def[index].tox);
	var toy := CInt(runes_def[index].toy);
	var toz := CInt(runes_def[index].toz);

	if( !tox && !toy && !toz )
		SendSysMessage( who, "Error 2: Blank rune!" );
		DestroyRune( runebook, runes_def, index );
		return 0;
	endif

	if( !CustomGate( who, tox, toy, toz ) )
		return 0;
	endif

	return 1;

endfunction


function SetDefaultLocation( runebook, byref runes_def, index, defloc := 0 )

	var old_prio := set_priority(100);

	if( !runes_def[index] )
		set_priority(old_prio);
		return 0;
	endif

	var tox := CInt(runes_def[index].tox);
	var toy := CInt(runes_def[index].toy);
	var toz := CInt(runes_def[index].toz);

	if( !tox && !toy && !toz )
		DestroyRune( runebook, runes_def, index );
		set_priority(old_prio);
		return 0;
	endif

	if( runes_def[defloc] )
		if( !ResetDefaultLocation( runebook, runes_def, defloc ) )
			set_priority(old_prio);
			return 0;
		endif
	endif

	SetObjProperty( runebook, "x", tox );
	SetObjProperty( runebook, "y", toy );
	SetObjProperty( runebook, "z", toz );
	runes_def[index].defloc := 1;

	set_priority(old_prio);
	return 1;

endfunction


function ResetDefaultLocation( runebook, byref runes_def, index )

	var old_prio := set_priority(100);

	if( !runes_def[index] )
		set_priority(old_prio);
		return 0;
	endif

	EraseObjProperty( runebook, "x" );
	EraseObjProperty( runebook, "y" );
	EraseObjProperty( runebook, "z" );
	runes_def[index].defloc := 0;

	set_priority(old_prio);
	return 1;

endfunction


function DropRune( who, runebook, byref runes_def, index )

	set_critical(1);

	UpdateRunesDef( runebook, runes_def );

	if( !runes_def[index] )
		SendSysMessage( who, "Error 1: Rune not found." );
		set_critical(0);
		return 0;
	endif

	if( runes_def[index].defloc )
		ResetDefaultLocation( runebook, runes_def, index );
	endif

	var rune := CreateItemInContainer( who.backpack, UOBJ_RUNE );
	if( !rune )
		SendSysMessage( who, "Error 2: Your backpack is full." );
		set_critical(0);
		return 0;
	endif

	var name := runes_def[index].name;
	var tox  := CInt(runes_def[index].tox);
	var toy  := CInt(runes_def[index].toy);
	var toz  := CInt(runes_def[index].toz);

	if( !runes_def.erase(index) )
		SendSysMessage( who, "Error 3: Couldn't erase the rune data from the book." );
		DestroyItem( rune );
		set_critical(0);
		return 0;
	endif

	if( !tox && !toy && !toz )
		SendSysMessage( who, "Error 4: Blank rune!" );
		DestroyItem( rune );
		set_critical(0);
		return 0;
	endif

	if( !name )
		name := "a marked recall rune";
	elseif( name != "a marked recall rune" )
		var splited := SplitWords( name );
		name := "A recall rune for";
		foreach part in splited
			name := name + " " + part;
		endforeach
	endif

	if( !SetObjProperty( rune, "x", tox ) &&
	    !SetObjProperty( rune, "y", toy ) &&
	    !SetObjProperty( rune, "z", toz ) )
	    	SendSysMessage( who, "Error 5: Couldn't copy the destination data on the rune!" );
	    	DestroyItem( rune );
	    	set_critical(0);
		return 0;
	endif

	SendSysMessage( who, "Rune " + name + " was successfuly dropped in your backpack." );

	rune.name := name;

	set_critical(0);
	return 1;

endfunction


function DestroyRune( runebook, byref runes_def, index )

	set_critical(1);

	UpdateRunesDef( runebook, runes_def );

	if( !runes_def[index] )
		set_critical(0);
		return 0;
	endif

	if( !runes_def.erase(index) )
		set_critical(0);
		return 0;
	endif

	set_critical(0);
	return 1;

endfunction


function UpdateRunesDef( runebook, byref runes_def )

	set_critical(1);

	var current_defs := GetObjProperty( runebook, "RuneDefs" );
	if( !current_defs )
		current_defs := array;
	endif

	runes_def := current_defs;

	set_critical(0);

endfunction


function GetSextantLatitude( y )

	var degrees := (y-1624) * 0.087890625;
	var minutes;
	var direction;

	if( degrees < 0 )
		direction := "N";
	else
		direction := "S";
	endif

	degrees := Abs( degrees );
	minutes := CInt((CInt((degrees-Floor(degrees))*100)*60)/100);

	return (Floor(degrees) + "o" + minutes + "'" + direction);

endfunction


function GetSextantLongitude( x )

	var degrees := (x-1323) * 0.0703125;
	var minutes;
	var direction;

	if( degrees < 0 )
		direction := "W";
	else
		direction := "E";
	endif

	degrees := Abs( degrees );
	minutes := CInt((CInt((degrees-Floor(degrees))*100)*60)/100);

	return (Floor(degrees) + "o" + minutes + "'" + direction);

endfunction


function TruncName( name )

	var the_name	:= "";
	var splited	:= SplitWords( name );

	foreach word in splited
		if( (len(word) + len(the_name)) < 15 )
			if( the_name == "" )
				the_name := word;
			else
				the_name := the_name + " " + word;
			endif
		else
			break;
		endif
	endforeach

	if( the_name == "" )
		the_name := name[1,16];
	endif

	return the_name;

endfunction

function NameValidation(thestring)
  var i, validstr := {"1", "2", "3", "4", "5", "6", "7", "8", "9", "0", "a", "b", "c",
  		      "d", "e", "f", "g", "h", "i", "j", "k", "l", "m", "n", "o", "p",
  		      "q", "r", "s", "t", "u", "v", "w", "x", "y", "z", ",", ".", "?",
  		      "/", "\\", "[", "]", "(", ")", "%", "$", "#", "@", "!", "^", "&",
  		      "*", ":", ";", "+", "-", "'", "`", " "};
  thestring:=lower(thestring);
  for(i:=1;i<=len(thestring);i:=i+1)
    if(!(thestring[i] in validstr)) return 0; endif
  endfor
  return 1;
endfunction
