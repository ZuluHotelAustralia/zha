use uo;
use os;
use cfgfile;
use math;
include "include/checkcity";
const MOVING_EFFECT_PURPLE_POTION  := 0x0f0d;

program use_explosion( character, potion )

    	if(!ReserveItem(potion))  return;  endif

	if( !Accessible( character, potion) )
		SendSysMessage( character, "You can't get to that!" );
		return;
	endif

	SendSysMessage( character, "Select a target" );
	var where := TargetCoordinates( character );


	if( CheckCity(character) )
		SendSysMessage( character, "You can't use that in town!" );
		return;
	endif

	if( !where )
		return;
	endif
	
	var multis := ListMultisInBox(where.x, where.y, where.z, where.x, where.y, where.z);

	if( !CheckLosAt( character, where.x, where.y, where.z )
	    || abs(where.z - character.z)>20 )
		SendSysMessage( character, "You can't see that!" );
		return;
	elseif (!character.multi && multis.size()!=0)
		SendSysMessage( character, "You can't throw it there, that would be naughty!" );
		return;
	endif

	PlayMovingEffectXYZ( character.x, character.y, character.z+15,
						 where.x, where.y, where.z,
						 MOVING_EFFECT_PURPLE_POTION, 8, 0, 0 );

	sleep(1);

	var parms := {};
	parms[1] := character;
	parms[2] := potion;
	parms[3] := where;

	start_script( ":alchemy:exploder", parms );

endprogram