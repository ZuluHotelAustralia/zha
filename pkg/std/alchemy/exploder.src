use uo;
use cfgfile;
use os;


include "include/checkcity";
include "include/drinkpotion";
include "include/spelldata";
include "include/client";
include "include/random";

program exploder(parms)

	var explosion_potions := { 0xdc07, 0xdc08, 0xdc09 };
	var thrower	:= parms[1];
	var potion	:= parms[2];
	var where	:= parms[3];

	var power := GetPotionStrength( potion );
	DestroyItem( potion );
	SetScriptController( thrower );

	if( CheckCity(thrower) )
		power := power - 1;
	endif

	if( power < 1 )
		power := 1;
	endif

	var lookout := CreateItemAtLocation( CInt(where.x), CInt(where.y), CInt(where.z), 0x0f0d, 1 );
	
	lookout.movable := 0;
	if(!CheckLineOfSight95(thrower, lookout))
		PrintTextAbove(thrower, thrower.name + " fumbles the potion!");
		where.x := thrower.x;
		where.y := thrower.y;
		where.z := thrower.z;
		Set_Critical(1);
		lookout.movable := 1;
		MoveObjectToLocation(lookout, thrower.x, thrower.y, thrower.z, _DEFAULT_REALM); //AutoConversion
		lookout.movable := 0;
		Set_Critical(0);
	else
		PrintTextAbove( lookout, "3");
		sleep(1);
		PrintTextAbove( lookout, "2");
		sleep(1);
		PrintTextAbove( lookout, "1");
		sleep(1);
	endif
	PlayObjectCenteredEffect( lookout, FX_SPELL_EXPLOSION, SPEED_SPELL_EXPLOSION, LOOP_SPELL_EXPLOSION );

	var location := GetStandingHeight(where.x, where.y, where.z);
	var okay;

	var dmg := (Random(9)+12) * power;
	foreach critter in ListMobilesNearLocationEx( CInt(where.x),CInt(where.y),CInt(where.z), power, LISTEX_FLAG_NORMAL + LISTEX_FLAG_HIDDEN );

		okay := 0;
		if (critter.multi || location.multi)
			if (critter.multi.serial == location.multi.serial)
				if ((CInt(critter.z) == CInt(location.z)) || (CInt(critter.z) < CInt(location.z)))
					okay := 1;
				endif
			endif
		else
			okay := 1;
		endif



		if (okay)
			PlayObjectCenteredEffect( critter, FX_SPELL_EXPLOSION, SPEED_SPELL_EXPLOSION, LOOP_SPELL_EXPLOSION );
			PlaySoundEffect( critter, SFX_SPELL_EXPLOSION );

			if( critter.isA(POLCLASS_NPC) )
				ApplyElementalDamageNoResist( critter, thrower, dmg, FIRE );
			else
				ApplyElementalDamageNoResist( critter, thrower, CInt(dmg/2), FIRE );
			endif
		endif
	endforeach
	DestroyItem( lookout );

	// REMOVED TO COUNTER MULTIPLE CORPSES BUG
	/*foreach item in ListItemsNearLocation( CInt(where.x), CInt(where.y), CInt(where.z), power )
		if( item.objtype in explosion_potions && item.movable )
			var parms2 := {};
			var loc := array;
			loc.+x;
			loc.+y;
			loc.+z;
			loc.x := item.x;
			loc.y := item.y;
			loc.z := item.z;
			parms2[1] := thrower;
			parms2[2] := item;
			parms2[3] := loc;
		
			start_script( ":alchemy:exploder", parms2 );
		endif
	endforeach*/

endprogram

    