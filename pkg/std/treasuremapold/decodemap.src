////////
// decodemap.src
//
// Checks the character's Cartogrphy skill to see if 
// the map can be read. If so, it creates a new, readable
// map and a digging tool. Also picks a random location
// for the treasure to be.
//
// Not in this script: Use the tool created and target the
// map to use and then the ground to dig for that treasure.
//
// Author: Racalac, 4/24/99
//
////////

use uo;
use cfgfile;

include "include/client";
include "include/attributes";
include "include/objtype";
include "include/random";

const UOBJECT_DIGGINGTOOL := 0x6008;

program decode_map( character, map )

	var cfgfile := ReadConfigFile( "itemdesc" );
	var elem := FindConfigElem( cfgfile, map.objtype );
	var maplevel := GetConfigInt( elem, "Level" );

	var maxlevel := GetMaxMapLevel( character );

	if(!ReserveItem(map))
		return;
	endif

	if( maplevel > maxlevel )
		SendSysMessage( character, "You cannot comprehend the map." );
	else
		if( CheckSkill( character, SKILLID_CARTOGRAPHY, -1, (maplevel*100) ) )
			CreateTreasureMap( character, map, maplevel );
		else
			SendSysMessage( character, "You understand the map, but fail to decode it." );
		endif
	endif


endprogram



function GetMaxMapLevel( character );

	var skill := GetEffectiveSkill( character, SKILLID_CARTOGRAPHY );

	if( skill >= 100 )
	   return 5;
	elseif( skill >= 80 )
	   return 4;
	elseif( skill >= 60 )
	   return 3;
	elseif( skill >= 40 )
	   return 2;
	elseif( skill >= 20 )
	   return 1;
	else
	   return 0;
	endif

endfunction


function CreateTreasureMap( character, oldmap, maplevel);

	var map := CreateItemInBackpack( character, UOBJ_MAP1, 1 );

	SetObjProperty( map, "level", maplevel );
	SetObjProperty( map, "decoder", character.serial );

	SetTreasureCoords( map, maplevel );

	SendSysMessage( character, "You successfully decode the map!" );

	DestroyItem( oldmap );

	var tool := CreateItemInBackpack( character, UOBJECT_DIGGINGTOOL, 1 );
	SetName( tool, "a Treasure digging tool" );

endfunction


function SetTreasureCoords( map, maplevel );

	var cfgfile := ReadConfigFile( "treasure" );
	var num := GetConfigMaxIntKey( cfgfile );

	var pick := (Random( num ) + 1);

	var elem := FindConfigElem( cfgfile, pick );

	var x := GetConfigInt( elem, "X" );
	var y := GetConfigInt( elem, "Y" );
	var z := GetConfigInt( elem, "Z" );

	SetObjProperty( map, "chestx", x );
	SetObjProperty( map, "chesty", y );
	SetObjProperty( map, "chestz", z );


	SetName( map, "a treasure map lvl" + maplevel );
	if( x < 265 )
	   map.xWest := 0;
	else
	   map.xWest := x-265;
	endif

	if( y < 260 )
	   map.yNorth := 0;
	else
	   map.yNorth := y-260;
	endif

	map.xEast := x+265;
	map.ySouth := y+260;
	map.gumpWidth := 200;
	map.gumpHeight := 200;

endfunction
