use uo;
use os;

include "include/spelldata";
include "include/attributes";
include "include/client";

program cast_chainlightning( parms )

	var circle := 7;
	var element := AIR;
	var caster;
	var cast_on;
	var fromhit;

	if (parms[1] == "#MOB")
		caster := parms[2];
		cast_on := parms[3];
		if (parms[4])
			circle := parms[4];
		endif
		if (parms[5])
			fromhit := parms[5];
		endif

	else
		caster := parms;
		cast_on := CanTargetArea(caster, circle);
		if (!cast_on)
			return;
		endif
	endif

	var dmg;
	var range;
	
	if (GetObjProperty(caster, "UsingBardInstrument")==1)
		range := GetEffectiveSkill(caster,SKILLID_ENTICEMENT)/15;

		if( GetObjProperty( caster, CLASSEID_BARD ) )
			range := CInt( range * CLASSE_BONUS );
		endif

	else
		range := GetEffectiveSkill(caster,SKILLID_MAGERY)/15;

		if( GetObjProperty( caster, CLASSEID_MAGE ) )
			range := CInt( range * CLASSE_BONUS );
		endif
	endif

	var victims := ListMobilesNearLocation( cast_on.x, cast_on.y, cast_on.z, range);
	foreach mobile in victims

		if( (CheckLosAt( mobile, cast_on.x, cast_on.y, cast_on.z ) ) )
			if (AutoCallGuardCheck(caster, mobile))
				return;
			endif
			var immunity := IsProtected( caster, mobile, circle );
			var circ := circle;
			if( immunity == IMMUNED )
				continue;
			endif

			if( immunity == CURSED )
				circ := circ * 2;
			endif
		
			dmg := CalcSpellDamage( caster, mobile, circ, AREA_EFFECT_SPELL );
			send_attack(mobile,caster, SPELLID_CHAIN_LIGHTNING);
			PlayLightningBoltEffect( mobile );
			PlaySoundEffect(mobile, SFX_SPELL_CHAIN_LIGHTNING);
			PlayLightningBoltEffect( mobile );
			ApplyElementalDamage( caster, mobile, circ, dmg, element );
		endif

    	endforeach

endprogram