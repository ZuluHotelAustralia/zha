use os;
use uo;
include "include/cmdlevel_check";

program textcmd_gotoserial( who, serial )
	if (!checkStaff(who, "gotoserial", CMDLEVEL_ADMINISTRATOR))
		return 0;
	endif

	serial := CInt(serial);
	if( !serial )
		return 0;
	endif

	var object := SystemFindObjectBySerial(serial);
	if( !object )
		object := SystemFindObjectBySerial(serial);
	endif
	
	if( object )
		var parent := object;
		while( parent.container )
			parent := parent.container;
		endwhile

		parent := SystemFindObjectBySerial(parent.serial);
		
		if (parent == object)
			SendSysMessage( who, "Object is " + object.name + "." );
			MoveObjectToLocation( who, object.x, object.y, object.z, _DEFAULT_REALM, MOVEOBJECT_FORCELOCATION ); //AutoConversion
		elseif (parent.isA(POLCLASS_MOBILE) && !parent.isA(POLCLASS_NPC))
			SendSysMessage( who, "Object is " + object.name + " and is on the Player: " + parent.name + ", Account: " + parent.acctname + ", at location: (" + parent.x + ", " + parent.y + ", " + parent.z + ")." );
		elseif (SplitWords(parent.name)[1] == "SecureChest")
			SendSysMessage( who, "Object is " + object.name + " and is in a secure chest " + Hex(splitWords(parent.name)[2]));
			var chest := SystemFindObjectBySerial(SplitWords(parent.name)[2]);
			MoveObjectToLocation( who, chest.x, chest.y, chest.z, _DEFAULT_REALM, MOVEOBJECT_FORCELOCATION ); //AutoConversion
		else
			var parent_owner_serial := CInt(SplitWords(parent.name)[3]);
			var owner := SystemFindObjectBySerial(parent_owner_serial);
			if(!owner )
				owner := SystemFindObjectBySerial(parent_owner_serial, SYSFIND_SEARCH_OFFLINE_MOBILES);
			endif
	
			SendSysMessage( who, "Object is " + object.name + " and is in the bankbox of Mobile " + Hex(parent_owner_serial) + ": " + owner.name + ", Account: " + owner.acctname + "." );
		endif
	else
		SendSysMessage( who, "Couldn't find an object with this serial." );
	endif

endprogram