/* Goes to offline mobiles               **
** Tells you their accounts and a        **
** few other things      **
** Seravy - seravy@gmail.com             */


use uo;
use os;

include "include/cmdlevel_check";

include "include/npcbackpacks";
include "util/bank";

program textcmd_goto(character, whom)
	if (!checkStaff(character, "goto", CMDLEVEL_COUNSELOR))
		return 0;
	endif

	whom := upper( whom );
	var chr := MakeAndProcessGump( character,whom);
	if( chr )
		MoveCharacterToLocation( character, chr.x, chr.y, chr.z, MOVECHAR_FORCELOCATION );
		SendSysMessage(character, chr.name+"'s account is '"+chr.acctname+"'");
		
		if (!chr.backpack)
			SendSysMessage(character, "Player has no backpack!");
			return;
		endif

		SendOpenSpecialContainer(character, chr.backpack );

		var bankbox := FindBankBox(chr);
		if( bankbox )
			SendOpenSpecialContainer(character, bankbox );
		endif
                //OpenPaperdoll(character, chr);

	endif

endprogram


function MakeAndProcessGump( who,whom)

	var layout	:= array( "page 0",
				  "resizepic 50  0   2600 300 525",
				  "resizepic 100 480 5100 200  25",
				  "text 155 30 5 0");
			
	var data	:= array( "Goto Menu");

	var playername;
	var lastindex:=0;
	var maxlen:=len(whom);
	var players;
        EnumerateAllCharacters(players);

	var start	:= 1;
	var count	:= 0;
	var done	:= 0;
	var page	:= 1;
	var perpage	:= 20;
	var y 	:= 70;
	var i;
	var uhunter;

	layout.append( "page 1" );

	for( i := start; i <= len(players); i := i + 1 )
		uhunter:=CInt(GetObjProperty(players[i],"UHunter"));
		playername:=upper(players[i].name);


		if ((playername[1,maxlen]==whom) && ( (uhunter==0)|| ( who.cmdlevel>3 ) ))
			if( done == perpage )
				done 	:= 0;
				page 	:= page + 1;
				y 	:= 70;
		
				layout.append( "button 250 481 5540 5541 0 " + (page ) ); // Next button for prev page
				layout.append( "page " + page );
				layout.append( "button 125 481 5537 5538 0 " + (page - 1) ); // Prev button for current page
			endif
	
			if(players[i].concealed <= who.cmdlevel)
					if (players[i].cmdlevel > 0)
					  layout.append( "text 85 " + y + " 51 " + len(data) );
					else
					  layout.append( "text 85 " + y + " 0 " + len(data) );
					endif
		
					layout.append( "button 300 " + (y+5) + " 2118 2117 1 0 " + i );
					data.append( players[i].name );
		
					y 	:= y + 20;
					done 	:= done + 1;
					count := count + 1;
					lastindex:=i;
			endif
		endif
	endfor

	if (count>0)
		var result;
		if (count==1)
			result:=lastindex;
		else
			result := SendDialogGump( who, layout, data );
			if( !result[0] )
				return;
			endif
			result := CInt( result[0] );
		endif

		return players[result];
	else
		SendSysMessage(who, "No player with name '"+ whom +"*' was found.");
		return;
	endif
endfunction

// BY Myrathi
// Returns an array of <OfflineMobileRef>'s for
// all characters in all accounts.
function EnumerateAllCharacters( byref zOutChars )

    // Clean result array
    zOutChars:= {};

    // Grab account names
    var zAccts := ListAccounts();

    // Loop through the account names
    foreach sAcct in zAccts

        // Get the account object (sanity check it)
        var oAcct := FindAccount( sAcct );
        if ( !oAcct )
            continue; // um.. doesn't exist? err...
        endif

        // Loop through the character slots and
        // add any we find to the Result Array.
        for slot := 1 to 5
            var oChar := oAcct.getcharacter( slot );
            // Returns 'error' if doesn't exist
            if ( oChar != error )
                zOutChars.append( oChar );
            endif
        endfor
        sleepms( 2 ); // no lag plzkthx!

    endforeach

    // Return the number of characters found
    return (zOutChars.size());

endfunction