use uo;
use os;
use cfgfile;

var REAGS := {0xf7a, 0xf7b, 0xf84, 0xf85, 0xf86, 0xf88, 0xf8c, 0xf8d};
var PAGANS := {0xf78, 0xf79, 0xf7c, 0xf7d, 0xf7e, 0xf7f, 0xf80, 0xf81, 0xf82, 0xf83, 0xf87, 0xf89, 0xf8a, 0xf8b, 0xf8e, 0xf8f, 0xf90, 0xf91};
var AMMO := {0xf3f, 0x6051, 0xb200, 0x1bfb, 0x824e};
var GEMS := {0xf0f, 0xf10, 0xf11, 0xf13, 0xf15, 0xf16, 0xf18, 0xf25, 0xf30, 0xc539, 0xc53a, 0xc538};

include "include/time";

program get_items_from_corpse(who, types)

	if (!types)
		SetDefaultBag(who);
		return;
	endif

	var typearray := SplitWords(types);

	for i:= 1 to len(typearray)
		typearray[i] := lower(typearray[i]);
	endfor

	foreach type in typearray
		if (type!="reags" && type!="pagans" && type!="ammo" && type!="gems" && type!="scrolls")
			SendSysMessage(who, "Usage: .get <reags> <pagans> <ammo> <gems> <scrolls>");
			return;
		endif
	endforeach

	SendSysMessage(who, "Select an NPC corpse to loot");
	var corpse := Target(who, TGTOPT_CHECK_LOS);
	if (!corpse)
		SendSysMessage(who, "Cancelled");
		return;
	elseif (Distance(who, corpse)>1)
		SendSysMessage(who, "You are too far away to do that");
		return;
	elseif (!corpse.isa(POLCLASS_CORPSE))
		SendSysMessage(who, "That's not a corpse!");
		return;
	elseif (!GetObjProperty(corpse, "npctemplate"));
		SendSysMessage(who, "That's not an NPC corpse!");
		return;
	endif

	var bagserial:=GetObjProperty(who, "DefaultBag");
	var bag:=0;
	if (!bagserial)
		bagserial:=SetDefaultBag(who);
		if (!bagserial)
			return;
		endif
	endif
	
	foreach item in EnumerateItemsInContainer(who.backpack)
		if (item.serial == bagserial)
			bag:=item;
			break;
		endif
	endforeach
	
	if (!bag)
		bagserial:=SetDefaultBag(who);
		if (!bagserial)
			return;
		endif

		foreach item in EnumerateItemsInContainer(who.backpack)
			if (item.serial == bagserial)
				bag:=item;
				break;
			endif
		endforeach
	endif
	
	if (!bag)
		return;
	endif
	
	foreach type in typearray
		case type
			"reags":	GrabItems(who, corpse, bag, REAGS);
			"pagans":	GrabItems(who, corpse, bag, PAGANS);
			"ammo":		GrabItems(who, corpse, bag, AMMO);
			"gems":		GrabItems(who, corpse, bag, GEMS);
			"scrolls":	GrabScrolls(who, corpse, bag);
		endcase
	endforeach

endprogram

function GrabItems(who, corpse, bag, type)

	SetObjProperty(who, "fastlooting", 1);
	foreach item in EnumerateItemsInContainer(corpse)
		if (item.objtype in type)
			if (CreateItemInContainer(bag, item.objtype, item.amount))
				SendSysMessage(who, "Grabbed "+item.desc);
				DestroyItem(item);
			else
				SendSysMessage(who, "Failed to move "+item.desc);
			endif
		endif
	endforeach
	EraseObjProperty(who, "fastlooting");

endfunction
	
function GrabScrolls(who, corpse, bag)

	var obj;
	var warned:=0;
	SetObjProperty(who, "fastlooting", 1);
	foreach item in EnumerateItemsInContainer(corpse)
		obj:=item.objtype;
		//if (!(obj.objtype==0xefa)) // spellbook
			//break;
		//else
		if (item.container.serial==corpse.serial)
			if ((obj >= 0x1f2d && obj <= 0x1f6c) || 
			(obj >= 0xa100 && obj <=0xa110) ||
			(obj >= 0xb100 && obj <= 0xb110))
				if (CreateItemInContainer(bag, obj, item.amount))
					SendSysMessage(who, "Grabbed "+item.desc);
					DestroyItem(item);
				else
					SendSysMessage(who, "Failed to move "+item.desc);
				endif
			endif
		elseif (item.container.objtype==0xefa && !warned)
			foreach player in EnumerateOnlineCharacters()
				if (player.cmdlevel)
					SendSysMessage(player, who.name+" is trying to use .get bug!", 3, 38);
				endif
			endforeach
			warned:=1;
			logcheat(who);
		endif
	endforeach
	EraseObjProperty(who, "fastlooting");
	
endfunction			

function SetDefaultBag(who)

	SendSysMessage(who, "Target a bag/backpack/pouch");
	var bag:= Target(who);
	if (!bag)
		SendSysMessage(who, "Cancelled");
		return;
	elseif (bag.container!=who.backpack)
		SendSysMessage(who, "The container must be in your backpack");
		return;
	elseif (bag.graphic != 0xe75 && bag.graphic != 0xe76 && bag.graphic != 0xe79)
		SendSysMessage(who, "Container must be a bag, backpack or pouch!");
		return;
	endif
	
	SetObjProperty(who, "DefaultBag", bag.serial);
	return bag.serial;
	
endfunction

function logcheat(who)
	var desc := who.name + " / " + who.acctname;
	
	var elemkey := who.acctname;
	var props := array;
	var prop := array;
	prop .+ pname;
	prop .+ pvalue;
	
	prop.pname := "charname";
	prop.pvalue := who.name;
	props[1] := prop;
	
	prop.pname := "serial";
	prop.pvalue := who.serial;
	props[2] := prop;
	
	prop.pname := "coords";
	prop.pvalue := CStr(who.x) + " " + CStr(who.y);
	props[3] := prop;
	
	prop.pname := "datetime";
	prop.pvalue := NowString();
	props[4] := prop;
	
	AppendConfigFileElem( "spellbookabusers", "haxor", elemkey, props );
	UnloadConfigFile("spellbookabusers");
endfunction