use uo;
use os;
use basic;
include "include/cmdlevel_check";

program disable_guild_chat(user, dur)

	if (!checkStaff(user, "gcban", CMDLEVEL_COUNSELOR))
		return 0;
	endif
	var duration:=CInt(dur);
	if (dur=="")
		SendSysMessage(user, "Usage: .gcban [minutes] (-1 for perma, 0 for unban)");
		return;
	endif
	var squelchtext:="";
	var who:= Target(user);
	if (!who.isa(POLCLASS_MOBILE))
		SendSysMessage(user, "Cancelled");
		return;
	elseif (user.cmdlevel <= who.cmdlevel)
		SendSysMessage(who, user.name+" tried to guildchat-ban you!", 3, 38);
		return;
	endif
	if (duration==-1)
		SetObjProperty(who, "#guildchatsquelch", -1);
		squelchtext:=who.name+" was guildchat-banned by "+user.name+" until further notice";
		if (user.cmdlevel < 3)
			SendSysMessage(who, "You have been guildchat-banned until further notice by "+user.name, 3, 38);
		endif
	elseif(duration==0)
		EraseObjProperty(who, "#guildchatsquelch");
		SendSysMessage(who, "You can guildchat again");
		SendSysMessage(user, who.name +" can now guild chat");
		return;
	else
		squelchtext:=who.name+" was guildchat banned by "+user.name+" for "+duration+" minutes";
		if (user.cmdlevel < 3)
			SendSysMessage(who, "You have been guildchat-banned by "+user.name+" for "+duration+" minutes", 3, 38);	
		endif
		duration:=CInt(duration*60);
		SetObjProperty(who, "#guildchatsquelch", ReadGameClock()+duration);
	endif

	if (user.cmdlevel < 3)
		foreach mobile in EnumerateOnlineCharacters()
			if (mobile.cmdlevel)
				SendSysMessage(mobile, squelchtext, 3, 38);
			endif
		endforeach	
	else
		SendSysMessage(user, squelchtext, 3, 38);
	endif
	
	SetObjProperty(who, "LastGCBanBy", user.name+" "+user.serial);
	
endprogram
	
	