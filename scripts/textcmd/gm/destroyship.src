//GM Barnabus 1/2/2003
//This script deletes the ship the gm is standing on while preserving ship's items.
//Staff chooses target that they are to be teled to.
//Staff is teled to target
//Offline mobiles teled to target
//Items on deck and in hold of ship are teled to target
//Note: items on deck which cannot be stacked will be required to be moved individually or
//destroyed. However this command could be called several times for it to work in this case.

use boat;
use uo;
include "include/yesno";
include "include/cmdlevel_check";


program textcmd_destroyShip( who )
	if (!checkStaff(who, "destroyship", CMDLEVEL_GAME_MASTER))
		return 0;
	endif
	
	//should bring up yes no gump
	var boat := SystemFindBoatBySerial(who.multi.serial);
	var newloc;
	
	if (!boat)
		SendSysMessage( who, "You must stand on the ship you want to destroy." );
		return 0;
	else
		//checks multi if ship
		if (! boat.hold)
			SendSysMessage( who, "You must stand on the ship you want to destroy." );
			return 0;
		endif
		//checks user really wants to delete ship
		if (!YesNo(who, "Delete ship?"))
			return;
		endif
	
	endif
	
	SendSysMessage( who, "Where do you wish items to be moved?" );
       	newloc := TargetCoordinates( who );
	if (newloc)
		MoveCharacterToLocation(who, newloc.x,newloc.y, newloc.z);
	else
		return;
	endif
	
    	if (boat.has_offline_mobiles)
        	SendSysMessage( who, "Boat has offline mobiles on the deck." );
         	var result := boat.move_offline_mobiles( newloc.x, newloc.y, newloc.z);
         	if(!result)
         		SendSysMessage(who, result.errortext);
         	endif
   	endif
	
	SendSysMessage( who, "Moving items in hold to target location." );
	var inhold := boat.hold;
	foreach item in EnumerateItemsInContainer( inhold );
		MoveItemToLocation(item, newloc.x,newloc.y, newloc.z);
	endforeach

	//var boattag := boat.multi;
	SendSysMessage( who, "Moving items on deck to target location." );
	foreach item in (boat.items) //move onboard items
		MoveItemToLocation(item, newloc.x,newloc.y, newloc.z);
	endforeach
	
	
	var res := DestroyMulti( boat );
  	if (res)
		SendSysMessage( who, "Ship successfully deleted." );
  	else
		SendSysMessage( who, "Can't delete: " + res.errortext );
  	endif
	return 1;
endprogram