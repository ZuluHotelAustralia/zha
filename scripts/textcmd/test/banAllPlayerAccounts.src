//Barnabus 30/10/3
use uo;
use os;
use basic;

include "include/cmdlevel_check";
include "include/yesno";


program textcmd_banAllPlayerAccounts( who )
	if (!checkStaff(who, "banAllPlayerAccounts", CMDLEVEL_DEVELOPER ))
		return 0;
	endif
	
  	SendSysMessage(who, "Banning all accounts without a staff character on them i.e. dont have a character of cmdlevel >=1." , 3,53);
	var accobj;
	if (!YesNo(who, "Ban all players?"))
		return 0;
	endif

	foreach accName in ListAccounts()
     		accobj := FindAccount(accName);
		if (! accobj.banned)
 	  		if (! HasCmdLevelCharacters(who, accobj, accName))
				accobj.ban();
				SendSysMessage(who, accName + " banned.",3,70);
			endif
		endif
	endforeach
	
	SendSysMessage(who, "All Accounts with only player characters on them have been banned.",3,53);
	SendSysMessage(who, "To do a full wipe now do .deleteBannedAccounts and then run the account cleaner.",3,53);
        
endprogram

function HasCmdLevelCharacters(who, account, accName)
	var charNum;
	var player;
	for (charNum:=1; charNum <=5; charNum := charNum+1)
		player:=account.GetCharacter(charNum);
		
		if (player != error && player.cmdlevel)
			return 1;
		endif
	endfor
	return 0;
endfunction



