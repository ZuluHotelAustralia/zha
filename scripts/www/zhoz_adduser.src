use http;
use uo;
use os;
use basic;

const AdminEmail := "harlz@zuluhotel.com.au";

program zha_createaccount()

	var Username := CStr(QueryParam( "account" ));
	var Password := CStr(QueryParam( "password" ));
//	var Password2 := CStr(QueryParam( "password2" ));
	var Email := CStr(QueryParam( "email" ));
	
	username := EscapeString(username);
	password := EscapeString(password);
//	password2 := EscapeString(password2);
	
	WriteHtml("<style type='text/css'> body{ background:none transparent; font-family: Georgia, 'Times New Roman', serif; font-size: 13px; line-height: 1.5em; margin-right: auto; margin-left: auto; margin-top: 40px; color: #625252;}</style> ");
	WriteHtml( "<html><head>");
	WriteHtml("<center>");
	WriteHtml("<hr></hr><FONT SIZE=4>Account Creation</FONT><br><hr></hr>");

	var Errors := array;
	
	CheckAccountName(Username, Errors);
	CheckPassword(Password, Errors);
	CheckEmail(Email, Errors);

	WriteHtml( "<body>" );
	if( Len(Errors) )
		
		WriteHtml( "<p>Sorry, there is a problem with your account details.</p>" );
		
		foreach ErrorCode in Errors
			SendError(ErrorCode, Username);
		endforeach
		
		WriteHtml( "<p>Please press the back button in your browser and try again.</p>");
	else
		var Account := CreateAccount(Username, Password, 0);
		
		if (!Account)
			SendCreationFailed(Username);
		else
			Account.enable();
			Account.setprop("Email", Email);
			SendSuccess(Username, Password, Email);
		endif
	endif
	
	WriteHtml("</center>");
	WriteHtml( "</body></html>");
endprogram

function SendCreationFailed(Username)

	foreach person in EnumerateOnlineCharacters()
		if (person.cmdlevel > 1)
			SendSysMessage(person, "Error creating account: " + username, 3, 33);
		endif
	endforeach
	
	WriteHtml( "<p>There was an error creating the account, please try again.</p>");
	WriteHtml( "<p><br>If the problem persists, <a href='mailto:" + AdminEmail + "'>email a staff member</a> or leave a post on the <a href='http://forums.zuluhotel.com.au' target='_blank'>forums</a>.");

endfunction

function SendSuccess(Username, Password, Email)

	
	WriteHtml( "<title>Zulu Hotel Australia Account Creation</title>" );
	WriteHtml( "<p>Welcome " + Username + "!</p>");

	WriteHtml( "<p>The account you requested is now active!  Below are your account details for <strong>Zulu Hotel Australia.</strong></p>");
	WriteHtml("<br><br>");
	WriteHtml( "<li>Username: <strong>" + Username + "</strong></li>");
	WriteHtml( "<li>Password: <strong>" + Password + "</strong></li>");
	WriteHtml( "<li>Email: <strong>" + Email + "</strong></li>");

	WriteHtml( "<p>Thank you for joining Zulu Hotel Australia,<br>");
	WriteHtml( "<p>We hope you enjoy your stay!<br>");

endfunction

function EscapeString(Str)

	var CleanedString := array;
	
	foreach AsciiChar in CAscZ(Str)

		if( ( AsciiChar >= 47 && AsciiChar <= 57 ) || ( AsciiChar >= 64 && AsciiChar <= 90) || ( AsciiChar >= 97 && AsciiChar <= 122)  ) //0-9 a-z A-Z
			CleanedString.append(AsciiChar);
		endif
		
	endforeach
	
	Str := CChrZ(CleanedString);
	
	return Str;

endfunction




function CheckAccountName(Username, byref Errors)

	if (!Username)
		Errors.append(1);
	endif

	if (TypeOf(Username) != "String" || Username["staff"])
		Errors.append(2);
	endif

	if (FindAccount(Username))
		Errors.append(3);
	endif

	if (len(Username) < 4)
		Errors.append(4);
	endif

	if (len(Username) > 15)
		Errors.append(5);
	endif

endfunction

function CheckPassword(password, byref Errors)

//	if ( password != password2)
//		Errors.append(7);
//	endif

	if (password)
		if ( Len (password) < 4)
			Errors.append(8);
		elseif ( Len(password) > 15)
			Errors.append(9);
		endif
	endif

endfunction

function CheckEmail(email, byref Errors)

	if (!email)
		Errors.append(10);
	endif

	if (!find(email, "@", 1))
		Errors.append(11);
	endif

	if (len(email)<10)
		Errors.append(12);
	elseif (len(email) > 60)
		Errors.append(13);
	endif

endfunction

function SendError(ErrorCode, Username)

	case (ErrorCode)
		1: WriteHtml( "<p><i>* No username was entered.</i></p>"); break;
		2: WriteHtml( "<p><i>* Error in the username, try again.</i></p>"); break;
		3: WriteHtml( "<p><i>* Username '<strong>" + Username + "</strong>' already exists.</i></p>"); break;
		4: WriteHtml( "<p><i>* Username is too short.</i></p>"); break;
		5: WriteHtml( "<p><i>* Username is too long.</i></p>"); break;
		6: WriteHtml( "<p><i>* Username '<strong>" + Username + "</strong>' is currently being processed.</i></p>"); break;
		7: WriteHtml( "<p><i>* Passwords do not match.</i></p>"); break;
		8: WriteHtml( "<p><i>* Password is too short.</i></p>"); break;
		9: WriteHtml( "<p><i>* Prror, password is too long.</i></p>"); break;
		10: WriteHtml( "<p><i>* You must enter an email address.</i></p>"); break;
		11: WriteHtml( "<p><i>* The email address doesn't appear to be valid.</i></p>"); break;
		12: WriteHtml( "<p><i>* The email address is too short.</i></p>"); break;
		13: WriteHtml( "<p><i>* The email address is too long.</i></p>"); break;
	endcase
	
endfunction
